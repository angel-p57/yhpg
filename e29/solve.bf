->+>>+>>,+[
 [
  [->+>[->>>]<[>+++++++>+>]<<<<]
  >>>--[---
   <<-<<+<[-[-[-
      ** 3 **
      <<[-
       ** e==1 **
       >>>->>>[-]+>-[+[-]
        ** not slash **
        <->
       ]
       <[-
        ** slash **
        <<<<+>>>>
       ]
      <<<<<<]
      >>>[-
       ** e==0 **
       >>>>>+<[-[[-]
         ** other **
         >-<<[-]<<<<+>>>[->+<]<++++[->+++++++++++<]>>>
        ]>[-
         ** slash **
         <<<<<<+>>>>>>
        ]
       <]>[-
         ** quote **
         <<[-<<<<<+>>>>>]<[-]<++++[->+++++++++++<]<<++<<+>>>>>>>>
       ]
      <<<<<]
     ]>[-
      ** 2 **
      <<[->+>>>>-<<<<<]>[-<+>]
      >>>>[+++++[-]>+<]+>[[-]
       ** not q **
       <-<<<<++<<[-]>>>>>>>
      ]<[-
       ** q **
       <[-]<<<<[-]>+>>>>
      ]
     <<]
    <]>[-
      ** 1 **
      >>>>>+<[-[[-]
        ** other **
        >-<<[-]<<<<+<<[-]>>>>>>>
       ]>[-
        ** slash **
        <<<[-]<<<+++>>>>>>
       ]
      <]>[-
        ** quote **
        <<[-<<<<<+>>>>>]<[-]<<<++>>>>>>
      ]
    <<<<]
   <]>[-
      ** 0 **
      >>[-]>[-]>[-]
   <<<]
   >[<<<[->+<]<[->+<]<[->+<]>>>>>[-<<<<<+>>>>>]>]
  <,+>>>]
  <[
   ** newline **
   [-]<[-]>
  ]
 <<]
 <<<[-]<[->>+<<]>+>-[+[-]
  ** wrong **
  <++++[-<+++++++++>]<.
 >>]
 <[-
  ** correct **
  <-<+[-<+]->+[-.>+]
 ]
 +[[-]<+]++++++++++.[-]
->+>>+>>,+]
